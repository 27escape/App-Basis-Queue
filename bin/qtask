#!/usr/bin/env perl
# PODNAME: qtask 'app basis queue' script to add things to task queues
# ABSTRACT: add things to queues, using App::Basis::Queue

=head1 SYNOPSIS

    # add a message to the work task queue
    > qtask -q work --type=task "process /some/file/path"

    # process an item in a task queue, exit status will determin if it is processed
    # the queue message is passed to the exec command in quotes
    # obviously there are security concerns around doing this, clean your inputs!
    > qtask -q work --type=task --exec "/command/to/run"

    # peek at work items in a task queue, --type=task is default for a peek
    > qtask --peek --count=10 -q work

    # add to queue from output of another process
    # '-' says read from STDIN
    some_script | qtask -q work -

    # to get full help use
    > qtask --help

=head1 DESCRIPTION

Add am item to a queue or process an item from a queue

config file is in ~/.abq

    queue:
      dsn: dbi:SQLite:/tmp/abq.sqlite
      user:
      password:


The queue entry holds information about the queue database that you want to connect to, this is
obviously a perl DBI style connection

=cut

#
# (c) Kevin Mulholland, moodfarm@cpan.org
# this code is released under the Perl Artistic License

# -----------------------------------------------------------------------------

use 5.10.0 ;
use strict ;
use warnings ;
use POSIX qw(strftime) ;
use App::Basis ;
use App::Basis::Config ;
use DBI ;
use App::Basis::Queue ;
use Date::Manip::Date ;
use feature 'say' ;
use Lingua::EN::Inflexion ;
# what about Data::Dumper::GUI or YAML::Tiny::Color
# use Data::Printer ;

# -----------------------------------------------------------------------------

use constant QUEUE_CONFIG => "$ENV{HOME}/.abq" ;
use constant FIVE_DAYS    => 5 * 24 * 3600 ;
use constant PEEK_DEFAULT => 10 ;
use constant EXEC_DEFAULT => 1 ;


# -----------------------------------------------------------------------------
# lets do the testing stuff with private variables
{
    my $testing = 0 ;
    sub set_testing
    {
        $testing = 1 ;
    }
    sub is_testing
    {
        $testing ;
    }

}

# -----------------------------------------------------------------------------

my $program = get_program() ;

# -----------------------------------------------------------------------------
# always create the datetime strings the same way
sub std_datetime
{
    my ($secs) = @_ ;
    $secs ||= time() ;
    return strftime( "%Y-%m-%d %H:%M:%S UTC", gmtime($secs) ) ;
}

# -----------------------------------------------------------------------------
# convert something like a datetime string or an epoch value into a standardised
# datetime string and epoch value

sub parse_datetime
{
    my ($datetime) = @_ ;
    state $date = Date::Manip::Date->new() ;
    my @ret ;

    if ( !$datetime ) {
        return wantarray ? ( undef, undef ) : undef ;
    } elsif ( $datetime =~ /^\d+$/ ) {
        # assume anything less than five days is a time into the future
        $datetime += time() if ( $datetime <= FIVE_DAYS ) ;
        @ret = ( std_datetime($datetime), $datetime ) ;
    } else {
        # so parse will parse in locale time not as UTC
        $date->parse($datetime) ;
        {
            # if we get a warning about converting the date to a day, there
            # must be a problem with parsing the input date string
            local $SIG{__WARN__} = sub {
                die "Invalid date, could not parse" ;
            } ;
            my $day = $date->printf("%a") ;
        }

        my $d2 = $date->printf("%O %Z") ;
        # reparse the date to get it into UTC, best way I could think of :(
        $date->parse($d2) ;

        # secs_since_1970_GMT is epoch
        @ret = (
            std_datetime( $date->secs_since_1970_GMT() ),
            $date->secs_since_1970_GMT()
        ) ;
    }

    return wantarray ? @ret : $ret[0] ;
}

# -----------------------------------------------------------------------------
# connect to the queue DB

sub connect_queue
{
    my ( $dsn, $user, $passwd, $qname ) = @_ ;
    my $dbh
        = DBI->connect( $dsn, $user, $passwd,
        { RaiseError => 1, PrintError => 0, AutoCommit => 1 } )
        or die "Could not connect to DB $dsn" ;

    if ( $dsn =~ /SQLite/i ) {
        $dbh->do("PRAGMA journal_mode = WAL") ;
        $dbh->do("PRAGMA synchronous = NORMAL") ;
    }

    my $queue = App::Basis::Queue->new(
        dbh           => $dbh,
        default_queue => $qname,
        debug         => 0,
    ) ;
    return $queue ;
}

# -----------------------------------------------------------------------------
# main

my $action ;

my %opt = init_app(
    help_text => "Simple script to queue messages for later action
        use perldoc $program to get the setup for the " . QUEUE_CONFIG . " config file
        if message is '-' input read from STDIN",
    help_cmdline => "message to send",
    options      => {
        'verbose|v' => 'Output useful information',
        'queue|q=s' => { desc => 'queue to add things to', required => 1 },
        'size|s'    => 'Disply the number of unprocessed items in a task queue',
        'peek|p=i' => { desc =>
            'Display the next few items in a task queue',
            },
        'exec|e=s' => {
            desc =>
                "command to run with the message, use count to limit, default "
                . EXEC_DEFAULT,
        },
        'activates|a=s' => {
            desc =>
                'Parsable UTC datetime after which the message should be valid',
        },
        'count|c=i' => 'Number of messages to read',
    }
) ;

my $msg = join( ' ', @ARGV ) ;
verbose( "first up msg is $msg") ;
if ( $opt{test} ) {
    set_verbose(1) ;
    set_testing(1) ;
}

show_usage( "No config file found", 1) if( ! -f QUEUE_CONFIG) ;

# lets have the config named after this program
my $cfg = App::Basis::Config->new(
    filename     => QUEUE_CONFIG,
    die_on_error => 1
) ;

my $q = $cfg->get("queue") ;
msg_exit( "Could not find valid config in " . QUEUE_CONFIG, 2 )
    if ( !$q ) ;

my ( $activates, $epoch ) = parse_datetime( $opt{activates} ) ;

$q->{prefix} ||= "/task" ;
$q->{prefix} .= "/" if ( $opt{queue} !~ /^\// ) ;
$opt{queue} = $q->{prefix} . $opt{queue} ;
$opt{queue} =~ s|//|/|g ;

my $theq
    = connect_queue( $q->{dsn}, $q->{user}, $q->{password}, $opt{queue} ) ;

msg_exit( "Could not connect to queue $q->{dsn}", 2 ) if ( !$theq ) ;

if ( !$theq ) {
    msg_exit( "Could not connect to queue $q->{dsn}", 2 ) ;
}

# get the things out of the way that are information only
# if asking for size or peeking, then there is no message adding or sending
if ( $opt{size} || $opt{peek} ) {
    my $s = $theq->queue_size() ;
    if ( $opt{size} ) {
        say inflect "<#n:$s> <N:items> <V:were> found in the queue" ;
    } else {
        if ($s) {
            my $count = 1 ;
            say "-" x 80 ;
            foreach my $msg ( $theq->peek( count => PEEK_DEFAULT ) ) {
                say $count++ . ":\n$msg->{data}" ;
                say "-" x 80 ;
            }
        } else {
            say "The queue is empty" ;
        }
    }
} elsif( $opt{exec}) {
    # read $opt{count} items from the queue and run them individually against the exec program


} else {

    # optionally ready from stdin, allowing piping to script
    if( $msg eq '-') {
        $msg = "" ;
        foreach (<STDIN>) {
            $msg .= $_ ;
        }
        # remove any final linefeed
        chomp $msg ;
    }
    # if we have a message then this should be added to the queue asap
    if ($msg) {
        my $resp = $theq->add(
            data      => { $msg },
            activates => $activates
        ) ;
    } else {
        verbose("Parameters are required, or message queue is empty") ;
        exit 0 ;
    }
}
